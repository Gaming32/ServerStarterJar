plugins {
    id 'java'
    id 'maven-publish'
    id 'net.neoforged.gradleutils' version '3.0.0'
    id 'me.modmuss50.mod-publish-plugin' version '0.5.1'
}

group = 'net.neoforged'
version = gradleutils.version

base {
    archivesName = 'server'
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}

jar {
    manifest.attributes([
            'Main-Class'          : 'net.neoforged.serverstarterjar.Main',
            'Premain-Class'       : 'net.neoforged.serverstarterjar.Agent',
            'Launcher-Agent-Class': 'net.neoforged.serverstarterjar.Agent',
            'Add-Exports'         : 'java.base/jdk.internal.loader',
            'Add-Opens'           : 'java.base/java.lang',
    ])
}

var lastCommitMessage = new ByteArrayOutputStream()
project.exec {
    commandLine('git log -1 --pretty=%B'.split(' '))
    standardOutput = lastCommitMessage
}
publishMods {
    file = tasks.jar.archiveFile
    modLoaders.addAll('forge', 'neoforge')
    getChangelog().set(lastCommitMessage.toString().trim())
    type = STABLE
    github {
        accessToken = providers.environmentVariable('MAVEN_USER')
        repository = 'neoforged/serverstarterjar'
        commitish = 'main'
    }
}

test.enabled = false
